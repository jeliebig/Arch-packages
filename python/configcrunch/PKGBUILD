# Maintainer: Julian Liebig <julian.liebig@tudock.de>

pkgname=python-configcrunch
_name=${pkgname#python-}
pkgver="1.2.0"
pkgrel=1
pkgdesc="Configuration parser based on YAML-Files with support for variables, overlaying and hierarchies"
arch=('any')
url="https://github.com/theCapypara/${_name}"
license=('MIT')
groups=()
depends=('python-schema')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'python-setuptools-rust')
checkdepends=('python-pytest' 'python-pyyaml')
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('3fbd875b80ac520c66acb8dded73f3ae4f24e892e00620e09a72b8d240c554bc')

build() {
    cd "${_name}-${pkgver}"

    python -m build --wheel --no-isolation
}

check(){
    cd "${_name}-${pkgver}/tests"
    
    pytest
}

package() {
    cd "${_name}-${pkgver}"
    
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
