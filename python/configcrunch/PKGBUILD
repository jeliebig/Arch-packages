# Maintainer: Julian Liebig <julian.liebig@tudock.de>

pkgname=python-configcrunch
_name=${pkgname#python-}
pkgver="1.0.5"
pkgrel=1
pkgdesc="Configuration parser based on YAML-Files with support for variables, overlaying and hierarchies"
arch=('any')
url="https://github.com/theCapypara/${_name}"
license=('MIT')
groups=()
depends=('python-schema')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools' 'python-setuptools-rust')
checkdepends=('python-pytest' 'python-pyyaml')
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('9edd9a24e0eb0f67e7c73d2f9d8b84ba2cbb9a4f0eccf54d26af55b5571b7d7f')

build() {
    cd "${_name}-${pkgver}"

    python -m build --wheel --no-isolation
}

check(){
    local python_version=$(python -c 'import sys; print("".join(map(str, sys.version_info[:2])))')
    cd "${srcdir}/${_name}-${pkgver}/build/lib.linux-${CARCH}-cpython-${python_version}"

    cp -a "${srcdir}/${_name}-${pkgver}/${_name}/tests/fixtures" "./${_name}/tests/"
    
    pytest
}

package() {
    cd "${_name}-${pkgver}"
    
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
