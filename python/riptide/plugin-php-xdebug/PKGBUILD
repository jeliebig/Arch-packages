# Maintainer: Julian Liebig <julian.liebig@tudock.de>

pkgname=python-riptide-plugin-php-xdebug
_name=${pkgname#python-}
pkgver="0.10.1"
pkgrel=1
pkgdesc="Tool to manage development environments for web applications using containers - PHP Xdebug integration"
arch=('any')
url="https://github.com/theCapypara/${_name}"
license=('MIT')
groups=('riptide')
depends=('python-riptide-lib' 'python-riptide-cli' 'python-click')
makedepends=(python-build python-installer python-wheel python-setuptools)
checkdepends=()
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('f6f778b71852587836baccc3bd13127757029b485b89c59e2be6b341b6948e89')

build() {
    cd "${_name}-${pkgver}"

    python -m build --wheel --no-isolation
}

package() {
    cd "${_name}-${pkgver}"
    
    python -m installer --destdir="$pkgdir" dist/*.whl
}
