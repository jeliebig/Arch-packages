# Maintainer: Julian Liebig <julian.liebig@tudock.de>

pkgname=python-riptide-proxy
_name=${pkgname#python-}
pkgver="0.10.0"
pkgrel=1
pkgdesc="Tool to manage development environments for web applications using containers - HTTP and WebSocket Reverse Proxy Server"
arch=('any')
url="https://github.com/theCapypara/${_name}"
license=('MIT')
groups=('riptide')
depends=('python-riptide-lib' 'python-tornado' 'python-click' 'python-prctl' 'python-certauth')
makedepends=(python-build python-installer python-wheel python-setuptools)
checkdepends=()
install=$pkgname.install
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz
        'riptide@.service')
sha256sums=('8aae8156edbee8fa01f9ceb9d69efef4cbd624c089adb9e90ffccef46063dc89'
            'cc13d0d41b8d8ffe2348d96db171402535d4be8f0f2dc5dadcc33f58f2bcd3b2')

build() {
    cd "${_name}-${pkgver}"

    python -m build --wheel --no-isolation
}

package() {
    cd "${_name}-${pkgver}"
    
    python -m installer --destdir="$pkgdir" dist/*.whl
    
    mkdir -p "$pkgdir/etc/systemd/system"
    cp ../riptide@.service "$pkgdir/etc/systemd/system/"
}
