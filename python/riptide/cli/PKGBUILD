# Maintainer: Julian Liebig <julian.liebig@tudock.de>

pkgname=python-riptide-cli
_name=${pkgname#python-}
pkgver="0.10.1"
pkgrel=1
pkgdesc="Tool to manage development environments for web applications using containers - CLI-Application"
arch=('any')
url="https://github.com/theCapypara/${_name}"
license=('MIT')
groups=('riptide')
depends=('python-click' 'python-rich' 'python-click-help-colors' 'python-setproctitle' 'python-riptide-lib')
makedepends=(python-build python-installer python-wheel python-setuptools)
checkdepends=()
install=$pkgname.install
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/refs/tags/${pkgver}.tar.gz)
sha256sums=('6f7d27d32f7a9fd31320854b73ba6965274b3a737132b03d0112ad3a041da0a6')

build() {
    cd "${_name}-${pkgver}"

    python -m build --wheel --no-isolation
}

package() {
    cd "${_name}-${pkgver}"
    
    python -m installer --destdir="$pkgdir" dist/*.whl

    # Remove riptide_upgrade since it uses pip
    rm "$pkgdir/usr/bin/riptide_upgrade"
}
